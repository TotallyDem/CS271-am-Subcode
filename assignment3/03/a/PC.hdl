// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS: // I'm going to add some comments this time since my naming scheme is trash
    Add16(a=s, b[0]=true, b[1..15]=false, out=si); // si = stored increment
    Mux16(a=s, b=si, sel=inc, out=io); // io = increment out
    Mux16(a=io, b=in, sel=load, out=lo); // lo = load out
    Mux16(a=lo, b=false, sel=reset, out=zo); // zo = zero out
    Or(a=load, b=inc, out=changed);
    Or(a=changed, b=reset, out=loadD);
    Register(in=zo, load=loadD, out=s, out=out); // s = stored
}
